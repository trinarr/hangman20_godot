extends Node

const ToggleKey := DevConsoleInternal.ToggleKey

var _console: DevConsoleInternal = null
var _console_ready := false
var _pending_calls: Array[Callable] = []

# ========== Onready ==========
func _ready() -> void:
	# Don't complete initialization if debug_only in release
	if not OS.is_debug_build():
		set_process_input(false)
		return

	# Initiate & call initial/pending calls
	_console = DevConsoleInternal.new()
	_console.ready.connect(func():
		_console_ready = true

		# Pass initial config & theme to internals
		_console.set_title_label(title_label)
		_console.set_use_default_commands(use_default_commands)
		_console.set_use_command_history(use_command_history)
		_console.set_view_default_commands(view_default_commands)
		_console.set_keep_size_after_closing(keep_size_after_closing)
		_console.set_keep_position_after_closing(keep_position_after_closing)
		_console.set_keep_topmost(keep_topmost)
		_console.set_toggle_keybind(toggle_keybind)
		_console.set_close_on_escape(close_on_escape)
		_console.set_command_autocomplete(command_autocomplete)
		_console.set_alpha(alpha)
		_console.set_header_background(header_background)
		_console.set_output_background(output_background)
		_console.set_selection_highlight(selection_highlight)
		_console.set_input_background(input_background)

		for call in _pending_calls: call.call()
		_pending_calls.clear()
	, CONNECT_ONE_SHOT)

	add_child(_console)

# ========== PRIVATE METHODS ==========
func _defer_or_call(action: Callable) -> void:
	if not OS.is_debug_build(): return
	if _console_ready: action.call()
	else: _pending_calls.append(action)

# ========== PUBLIC METHODS ==========
# Command & Signal Registration
func add_command(command_name: String, callback: Callable) -> void: _defer_or_call(func(): _console.add_command(command_name, callback))

func add_signal(signal_name: String, target_signal: Signal) -> void: _defer_or_call(func(): _console.add_signal(signal_name, target_signal))

func delete_command(command_name: String) -> void: _defer_or_call(func(): _console.delete_command(command_name))

func delete_signal(signal_name: String) -> void: _defer_or_call(func(): _console.delete_signal(signal_name))

# Command & Signal Registry Getters
func has_command(command_name: String) -> bool:
	if _console_ready: return _console.has_command(command_name)
	return false

func has_signal_connected(signal_name: String) -> bool:
	if _console_ready: return _console.has_signal_connected(signal_name)
	return false

func get_commands() -> Dictionary[String, Callable]:
	if _console_ready: return _console.get_commands()
	return { }

func get_signals() -> Dictionary[String, Dictionary]:
	if _console_ready: return _console.get_signals()
	return { }

# Output
func print_line(text: String) -> void: _defer_or_call(func(): _console.output_callback(text))

func print_error(text: String) -> void: _defer_or_call(func(): _console.output_error(text))

func print_warning(text: String) -> void: _defer_or_call(func(): _console.output_warning(text))

func clear_output() -> void: _defer_or_call(func(): _console.clear_output())

# ========== PROPERTIES ==========
# Runtime
var visible := false:
	get:
		return _console.visible if _console_ready else visible
	set(value):
		visible = value
		_defer_or_call(func(): _console.visible = value)

# Configuration
var title_label := ProjectSettings.get_setting("dev_console/configuration/title_label", "CONSOLE"):
	set(value):
		title_label = value
		_defer_or_call(func(): _console.set_title_label(value))

var use_default_commands := ProjectSettings.get_setting("dev_console/configuration/use_default_commands", true):
	set(value):
		use_default_commands = value
		_defer_or_call(func(): _console.set_use_default_commands(value))

var use_command_history := ProjectSettings.get_setting("dev_console/configuration/use_command_history", true):
	set(value):
		use_command_history = value
		_defer_or_call(func(): _console.set_use_command_history(value))

var view_default_commands := ProjectSettings.get_setting("dev_console/configuration/view_default_commands", true):
	set(value):
		view_default_commands = value
		_defer_or_call(func(): _console.set_view_default_commands(value))

var keep_size_after_closing := ProjectSettings.get_setting("dev_console/configuration/keep_size_after_closing", false):
	set(value):
		keep_size_after_closing = value
		_defer_or_call(func(): _console.set_keep_size_after_closing(value))

var keep_position_after_closing := ProjectSettings.get_setting("dev_console/configuration/keep_position_after_closing", false):
	set(value):
		keep_position_after_closing = value
		_defer_or_call(func(): _console.set_keep_position_after_closing(value))

var keep_topmost := ProjectSettings.get_setting("dev_console/configuration/keep_topmost", true):
	set(value):
		keep_topmost = value
		_defer_or_call(func(): _console.set_keep_topmost(value))

var toggle_keybind := ProjectSettings.get_setting("dev_console/configuration/toggle_keybind", ToggleKey.QUOTE_LEFT):
	set(value):
		toggle_keybind = value
		_defer_or_call(func(): _console.set_toggle_keybind(value))

var close_on_escape := ProjectSettings.get_setting("dev_console/configuration/close_on_escape", true):
	set(value):
		close_on_escape = value
		_defer_or_call(func(): _console.set_close_on_escape(value))

var command_autocomplete := ProjectSettings.get_setting("dev_console/configuration/command_autocomplete", true):
	set(value):
		command_autocomplete = value
		_defer_or_call(func(): _console.set_command_autocomplete(value))

# Theme
var alpha := ProjectSettings.get_setting("dev_console/theme/console_transparency", 0.9):
	set(value):
		alpha = clampf(value, 0.5, 1.0)
		_defer_or_call(func(): _console.set_alpha(value))

var header_background := ProjectSettings.get_setting("dev_console/theme/header_background", Color(0.204, 0.204, 0.204, 1.0)):
	set(value):
		header_background = value
		_defer_or_call(func(): _console.set_header_background(value))

var output_background := ProjectSettings.get_setting("dev_console/theme/output_background", Color(0.137, 0.137, 0.137, 1.0)):
	set(value):
		output_background = value
		_defer_or_call(func(): _console.set_output_background(value))

var selection_highlight := ProjectSettings.get_setting("dev_console/theme/selection_highlight", Color(0.204, 0.204, 0.204, 0.878)):
	set(value):
		selection_highlight = value
		_defer_or_call(func(): _console.set_selection_highlight(value))

var input_background := ProjectSettings.get_setting("dev_console/theme/input_background", Color(0.114, 0.114, 0.114, 1.0)):
	set(value):
		input_background = value
		_defer_or_call(func(): _console.set_input_background(value))
